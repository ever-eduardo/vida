/*
* Merge Sort
*/


var p = print


var rand = load("rand")


var iComparable = {
    comparedTo = fun self, r {
        if self.val < r.val {
            ret -1
        }
        if self.val > r.val {
            ret 1
        }
        ret 0
    }
}


var iPrinter = {
    print = fun self {
        p(self.val)
    }
}


var data = {val = nil, comparedTo = iComparable.comparedTo, print = iPrinter.print}


var makeData = fun N, rnd, upperLimit {
    var xs = list(N)
    for i in 0, N {
        var c = clone(data)
        c.val = rnd(upperLimit)
        xs[i] = c
    }
    ret xs
}


var printData = fun xs {
    for _, v in xs {
        v->print()
    }
}


var less = fun v, w {
    if v->comparedTo(w) < 0 {
        ret true
    }
    ret false
}


var swap = fun comparables, i, j {
    var temp = comparables[i]
    comparables[i] = comparables[j]
    comparables[j] = temp
}


var isSorted = fun comparables, lo, hi {
    for i in lo+1, hi {
        if less(comparables[i], comparables[i-1]) {
            ret false
        }
    }
    ret true
}


var merge = fun comparables, auxList, lo, mid, hi {
    for k in lo, hi+1 {
        auxList[k] = comparables[k]
    }

    var i = lo
    var j = mid+1

    for k in lo, hi+1 {
        if i > mid {
            comparables[k] = auxList[j]
            j = j + 1
        } else if j > hi {
            comparables[k] = auxList[i]
            i = i + 1
        } else if less(auxList[j], auxList[i]) {
            comparables[k] = auxList[j]
            j = j + 1
        } else {
            comparables[k] = auxList[i]
            i = i + 1
        }
    }
}


let sort = fun comparables, auxList, lo, hi {
    if hi <= lo {
        ret nil
    }
    var mid = lo + (hi - lo) / 2
    sort(comparables, auxList, lo, mid)
    sort(comparables, auxList, mid+1, hi)
    merge(comparables, auxList, lo, mid, hi)
}


var mergeSort = fun comparables {
    var N = len(comparables)
    var auxList = list(N)
    sort(comparables, auxList, 0, N-1)
}


var N = 250_000
var upperLimit = N*5
var xs = makeData(N, rand.nextI, upperLimit)
mergeSort(xs)
p(isSorted(xs, 0, N))
